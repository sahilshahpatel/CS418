<!DOCTYPE HTML> 
<html lang="en"> 
<head>
<title>MP2: Terrain</title>
<meta charset="utf-8"> 
</head>

<!-- If you add additional .js files, be sure to add them here. -->
<script src="gl-matrix-min.js"></script>
<script src="Terrain.js"></script>
<script src="MP2.js"></script>

<!-- MP2: Modify the shaders to implement Phong shading instead of Gourand! -->
<script id="shader-vs" type="x-shader/x-vertex">#version 300 es
// Vertex Shader

// Use high-precision floats if available on this device.
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

in vec3 aVertexPosition;
in vec3 aVertexNormal;

uniform vec2 heightRange;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform mat3 normalMatrix;

out vec3 vertexNormalView;
out vec3 vertexPositionView;
out vec3 kAmbient;
out vec3 kDiffuse;

vec3 bezier(vec3 start, vec3 end, float t){
    return start * (1.0 - t) + end * t;
}

vec3 bezier2(vec3 start, vec3 mid, vec3 end, float t){
    return bezier(bezier(start, mid, t), bezier(mid, end, t), t);
}

vec3 bezier3(vec3 start, vec3 k1, vec3 k2, vec3 end, float t){
    return bezier(bezier2(start, k1, k2, t), bezier2(k1, k2, end, t), t);
}

void main(void) {
    // Transform the vertex position and normal to view coordinates
    vertexPositionView =(modelViewMatrix * vec4(aVertexPosition, 1.0)).xyz;
    vertexNormalView = normalize(normalMatrix * aVertexNormal);

    /* Determine material properties from height */
    float height = (aVertexPosition.z - heightRange.x) / (heightRange.y - heightRange.x);

    vec3 darkWater = vec3(0.0, 0.0, 0.2);
    vec3 water = vec3(0.0, 0.0, 0.8);
    vec3 sand = vec3(0.6, 0.6, 0.4);
    vec3 grass = vec3(0.2, 1.0, 0.0) * 0.5;
    vec3 dirt = vec3(0.1, 0.1, 0.0);

    float waterline = 0.25;
    float shoreline = 0.4;

    float inWater = 1.0 - step(waterline, height);
    float inShore = step(waterline, height) - step(shoreline, height);
    float inLand = step(shoreline, height);

    kDiffuse = inWater * bezier(darkWater, water, height / waterline)
             + inShore * bezier2(water, sand, grass, (height - waterline) / (shoreline - waterline))
             + inLand  * bezier2(grass, dirt, dirt, (height - shoreline) / (1.0 - shoreline));
    
    kAmbient = bezier(vec3(0.0), vec3(0.1), height);

    gl_Position =
        projectionMatrix * modelViewMatrix * vec4(aVertexPosition, 1.0);
}
</script>

<!-- MP2: Modify the shaders to implement Phong shading instead of Gourand! -->
<script id="shader-fs" type="x-shader/x-fragment">#version 300 es
// Fragment Shader

// Use high-precision floats if available on this device.
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

in vec3 kAmbient;
in vec3 kDiffuse;
uniform vec3 kSpecular;
uniform float shininess;

uniform vec3 lightPosition;
uniform vec3 ambientLightColor;
uniform vec3 diffuseLightColor;
uniform vec3 specularLightColor;

in vec3 vertexPositionView;
in vec3 vertexNormalView;
in float height;

out vec4 fragmentColor;


void main(void) {
    // The camera is at the origin in view coordinates
    vec3 cameraPositionView = vec3(0.0, 0.0, 0.0);
    
    // Calculate the three other vectors we need: l, r, and v
    vec3 lightVector = normalize(lightPosition - vertexPositionView);
    vec3 reflectionVector = normalize(reflect(-lightVector, vertexNormalView));
    vec3 viewVector = normalize(cameraPositionView - vertexPositionView);

    // Calculate diffuse light weighting: (n dot l)
    float diffuseWeight = max(dot(vertexNormalView, lightVector), 0.0);
    
    // Calculate the specular light weighting: (r dot v)^(shininess)
    float rDotV = max(dot(reflectionVector, viewVector), 0.0);
    float specularWeight = pow(rDotV, shininess);

    // Sum up all three lighting components into the color for the vertex,
    // and send it to the fragment shader.
    fragmentColor = vec4((  kAmbient * ambientLightColor
                        + kDiffuse * diffuseLightColor * diffuseWeight
                        + kSpecular * specularLightColor * specularWeight), 1.0);
}
</script>


<body onload="startup();">
    <canvas id="glCanvas" width="1200" height="600"></canvas>

    <form id="input_form">
        <fieldset>
            <legend>Rendering Options</legend>
            <div>
                <input type="radio" name="primitive" id="wireframe" value="wireframe"> Wireframe
                <input type="radio" name="primitive" id="polygon" value="polygon" checked> Polygon
                <input type="radio" name="primitive" id="wirepoly" value="wirepoly" > Polygon with Edges
            </div>
        </fieldset>
    </form>
</body>

</html>